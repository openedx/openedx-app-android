import org.yaml.snakeyaml.Yaml

buildscript {

    dependencies {
        classpath 'org.yaml:snakeyaml:1.33'
    }
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id "com.google.firebase.crashlytics"
}

def config_file = System.getenv("OPENEDX_ANDROID_CFG_FILE") ?: "config.yaml"
def config = new Yaml().load(new File(config_file).newInputStream())

android {
    compileSdk 34

    defaultConfig {
        applicationId config?.application_id ?: "org.openedx.app"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        resourceConfigurations += ["en", "uk"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    namespace 'org.openedx.app'

    flavorDimensions += "env"
    productFlavors {
        prod {
            dimension 'env'
        }
        develop {
            dimension 'env'
        }
        stage {
            dimension 'env'
        }
    }
    sourceSets {
        prod {
            def envMap = config.environments.find { it.key == "PROD" }
            if (envMap.value.RES_DIR) {
                res.srcDirs = [envMap.value.RES_DIR]
            }
        }
        develop {
            def envMap = config.environments.find { it.key == "DEV" }
            if (envMap.value.RES_DIR) {
                res.srcDirs = [envMap.value.RES_DIR]
            }
        }
        stage {
            def envMap = config.environments.find { it.key == "STAGE" }
            if (envMap.value.RES_DIR) {
                res.srcDirs = [envMap.value.RES_DIR]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_compiler_version"
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation project(path: ':core')
    implementation project(path: ':auth')
    implementation project(path: ':course')
    implementation project(path: ':dashboard')
    implementation project(path: ':discovery')
    implementation project(path: ':profile')
    implementation project(path: ':discussion')

    kapt "androidx.room:room-compiler:$room_version"

    implementation 'androidx.core:core-splashscreen:1.0.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "io.mockk:mockk-android:$mockk_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "androidx.arch.core:core-testing:$android_arch_version"
}